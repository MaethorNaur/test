plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

apply plugin: 'checkstyle'

check.dependsOn('checkstyle')

checkstyle {
    toolVersion = '10.12.0'
}

android {
    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    
    def versionMajor = versionProps['MAJOR'].toInteger()
    def versionMinor = versionProps['MINOR'].toInteger()
    def versionPatch = versionProps['PATCH'].toInteger()
    def versionRc = versionProps['RC'].toInteger()

    namespace 'tech.maethornaur.myapplication'
    compileSdk 33
    defaultConfig {
        applicationId "tech.maethornaur.myapplication"
        minSdk 24
        targetSdk 33
        versionCode versionMajor * 1000000 + 10000 * versionMinor + 100 * versionPatch - versionRc
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resValue "string", "app_name_with_flavour", "My Application"
        
        if(versionRc > 0) {
            versionNameSuffix "-rc.${versionRc}"
            resValue "string", "app_name_with_flavour", "My Application RC"
        }
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

   
    if (System.getenv("VIM8_BUILD_KEYSTORE_FILE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("VIM8_BUILD_KEYSTORE_FILE"))
                storePassword System.getenv("VIM8_BUILD_KEYSTORE_PASSWORD")
                keyAlias System.getenv("VIM8_BUILD_KEY_ALIAS")
                keyPassword System.getenv("VIM8_BUILD_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (System.getenv("VIM8_BUILD_KEYSTORE_FILE") != null) {
                signingConfig signingConfigs.release
            }

            create("rc") {
                initWith release
                applicationIdSuffix ".rc"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    
    buildFeatures {
        viewBinding true
    }

    lint {
        abortOnError true
        disable 'ObsoleteLintCustomCheck', 'ClickableViewAccessibility', 'VectorPath', 'UnusedResources', 'GradleDependency', 'OldTargetApi'
        htmlReport true
        warningsAsErrors true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.6.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.6.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task checkstyle(type: Checkstyle) {
    showViolations = true
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'

    classpath = files()
}
